buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = project.file('../version.properties').text.replace('version=', '')

repositories {
    mavenCentral()
}

jar {
    baseName = 'place-backend'
    archiveName = "place-backend.jar"
    manifest {
        attributes 'Implementation-Vendor': 'alexburchak.org',
                'Implementation-Title': 'Place Backend',
                'Implementation-Version': version,
                'Built-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Host': InetAddress.getLocalHost().getHostName()
    }
}

dependencies {
    compile project(":place-common")

    compile 'ch.qos.logback:logback-classic'
    compile 'ch.qos.logback:logback-core'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-actuator'
    compile 'org.springframework.kafka:spring-kafka:1.3.2.RELEASE'
    compile 'org.quartz-scheduler:quartz:2.3.0'
    compile 'org.hibernate:hibernate-validator'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.testng:testng:6.13.1'
}

test {
    useTestNG()
}

task compose(type: Copy) {
    description = 'Copy compose files'
    group "Build"

    into("${rootProject.buildDir}")
    into("/compose/place-backend") {
        from(jar)
    }
}

task zip(type: Zip) {
    description = 'Create zip distribution'
    group "Build"

    archiveName = "${jar.baseName}.zip"

    from("${rootProject.projectDir}/compose/place-backend/bin/") {
        into('place-backend/bin/')
    }
    from("src/main/conf/") {
        into('place-backend/conf/')
    }
    from(jar) {
        into('place-backend/lib/')
    }
}

build.finalizedBy(compose, zip)
